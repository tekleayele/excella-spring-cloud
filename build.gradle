
buildscript {
    ext.setExtProperty = { propertyName, defaultValue ->
        if (!project.hasProperty(propertyName)) {
            ext[propertyName] = defaultValue
        }
    }
    ext {
        setExtProperty("scsStartersVersion","1.3.1.RELEASE")
        setExtProperty("springCloudVersion","Camden.SR3")
        springBootVersion = "1.4.2.RELEASE"
        lombokVersion = '1.18.2'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/plugins-release" }
        maven { url "https://repo.spring.io/libs-release" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}
subprojects {
    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://repo.spring.io/libs-snapshot" }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:${scsStartersVersion}"

        }
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-actuator")
        compileOnly("org.projectlombok:lombok:${lombokVersion}")
        annotationProcessor("org.projectlombok:lombok:${lombokVersion}")

    }
}


project(":excella-config-server") {
    dependencies {
        compile('org.springframework.cloud:spring-cloud-config-server')
    }
}

project(":excella-service-provider") {
    dependencies {
        compile("io.pivotal.spring.cloud:spring-cloud-services-starter-config-client")
        compile("io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry")
    }
}

project(":excella-eureka-server") {
    dependencies {
        compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-eureka-server', version: '2.0.1.RELEASE'
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }
}

project(":excella-service-consumer") {
    dependencies {
        compile("io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry")
        compile("io.pivotal.spring.cloud:spring-cloud-services-starter-circuit-breaker")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.1'
}